import g4p_controls.*;

Metrics m;
World w;
boolean loop = true;

// Panning and zooming variables
float xOff = 0, yOff = 0;
float scale = 1.0;

void setup() {
  size(1280, 720);
  //frameRate(15);
  w = new World(width, height, 10, 10);
  m = new Metrics();
  createGUI();

  // Setting initial values from GUI
  SENSE_EXP = senseSlider.getValueF();
  SPEED_EXP = speedSlider.getValueF();
  SIZE_EXP = sizeSlider.getValueF();
  INITIAL_ENERGY = iniEnergySlider.getValueF();
  WANDER_STRENGTH = wanderStrengthSlider.getValueF();
  BABY_THRESH = babyThresholdSlider.getValueF();
  MUT_SD = mutationRateSlider.getValueF();
}

void draw() {
  background(0);
  scale(scale);
  
  // For pan and zoom 
  translate(xOff, yOff);
  w.update(m);
}

// Zooming in and out
void mouseWheel(MouseEvent e) {
  float value = e.getCount();
  float prevX = mouseX();
  float prevY = mouseY();
  if (value < 0) {
    scale *= 1.2;
  } else if (value > 0) {
    scale /= 1.2;
  }
  xOff -= (prevX - mouseX());
  yOff -= (prevY - mouseY());
}
float mouseX() {
  return mouseX / scale - xOff;
}

float mouseY() {
  return mouseY / scale - yOff;
}

// Panning 
void mouseDragged() {
    xOff -= (pmouseX - mouseX) / this.scale;
    yOff -= (pmouseY - mouseY) / this.scale;
  }
